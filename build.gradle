buildscript {
    dependencies {
        classpath 'com.vmlens:standalone:_'
    }
}

plugins {
    id 'java'
    id "me.champeau.jmh"
    id "io.morethan.jmhreport"
}

group = 'org.example'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation Testing.assertj.core
    testImplementation Testing.junit.jupiter
    testRuntimeOnly "org.junit.platform:junit-platform-launcher"
    testImplementation "net.jqwik:jqwik:_"

    implementation 'org.apache.commons:commons-lang3:_'
    implementation 'com.google.guava:guava:_'
    implementation 'jakarta.annotation:jakarta.annotation-api:_'
    implementation 'com.google.code.findbugs:jsr305:_'
    testImplementation 'org.awaitility:awaitility:_'
    testImplementation 'com.vmlens:api:_'

    implementation 'org.apache.commons:commons-numbers-core:_'

    jmh 'org.openjdk.jmh:jmh-core:_'
    jmh 'org.openjdk.jmh:jmh-generator-annprocess:_'
    jmh 'org.openjdk.jmh:jmh-generator-bytecode:_'
}

import com.vmlens.gradle.VMLens

tasks.register("vmlensReport") {
    doLast {
        new VMLens().process(layout.buildDirectory.getAsFile().get())
    }
}

test {
    doFirst{
        jvmArgs new VMLens().setup(layout.buildDirectory.getAsFile().get())
    }

    // VMLens currently does not work with jacoco
    jvmArgumentProviders.removeIf { it::class.java.simpleName == "JacocoAgent" }
    useJUnitPlatform {
        includeEngines "jqwik", "junit-jupiter"
    }
    finalizedBy("vmlensReport")
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(24)
    }
}

compileJava {
    // To enable argument names in reporting and debugging
    options.compilerArgs += '-parameters'
}

tasks.withType(JavaCompile).configureEach {
    it.options.compilerArgs += "--enable-preview"
}

tasks.withType(Test).configureEach {
    it.jvmArgs += "--enable-preview"
}

tasks.withType(JavaExec).configureEach {
    it.jvmArgs += '--enable-preview'
}

jmh {
    includes = ["ThreadSleep0Benchmark\\."]
    threads = Integer.getInteger("jmhThreads", 1)
    resultFormat = "JSON"
    //profilers = ["jfr"]
}

jmhReport {
    jmhResultPath = project.file('build/results/jmh/results.json')
    jmhReportOutput = project.file('build/results/jmh/report')
}

tasks.jmh.finalizedBy tasks.jmhReport

tasks.jmhReport.doFirst {
    project.file('build/results/jmh/report').mkdirs()
}